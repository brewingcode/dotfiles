#!/usr/bin/perl
use strict;
use warnings;
use Tie::File;
use Array::Diff;

# parse the output of "sudo netstat -panut", eg:
#
# Active Internet connections (servers and established)
# Proto Recv-Q Send-Q Local Address           Foreign Address         State       PID/Program name
# tcp        0      0 0.0.0.0:45632           0.0.0.0:*               LISTEN      10309/sshd: alex
# tcp        0      0 0.0.0.0:64995           0.0.0.0:*               LISTEN      9992/0          
# tcp        0      0 0.0.0.0:80              0.0.0.0:*               LISTEN      11372/apache2   
# tcp        0      0 0.0.0.0:9876            0.0.0.0:*               LISTEN      14783/openvpn   
# tcp        0      0 0.0.0.0:53              0.0.0.0:*               LISTEN      14773/openvpn   
# tcp        0      0 0.0.0.0:22              0.0.0.0:*               LISTEN      29143/sshd      
# tcp        0      0 127.0.0.1:60024         0.0.0.0:*               LISTEN      16643/sshd: alex
# tcp        0      0 127.0.0.1:25            0.0.0.0:*               LISTEN      936/master      
# tcp        0      0 0.0.0.0:443             0.0.0.0:*               LISTEN      11372/apache2   
# tcp        0      0 0.0.0.0:25565           0.0.0.0:*               LISTEN      16447/sshd: anyone
# tcp        0      0 10.212.122.78:64995     180.216.55.237:52514    FIN_WAIT2   9992/0          
# tcp        0      0 10.212.122.78:22        174.24.133.72:60526     ESTABLISHED 10305/sshd: alex [p
# tcp        0      0 10.212.122.78:22        174.24.133.72:47699     ESTABLISHED 16443/sshd: anyone 
# tcp        0      0 10.212.122.78:22        174.24.133.72:47711     ESTABLISHED 16639/sshd: alex [p
# tcp        0     64 10.212.122.78:22        174.24.133.72:59556     ESTABLISHED 9988/sshd: alex [pr
# tcp        0      0 10.212.122.78:64995     77.222.174.73:53386     TIME_WAIT   -               
# tcp        0      0 10.212.122.78:64995     110.169.240.223:28767   FIN_WAIT2   9992/0          
# tcp        0      0 10.212.122.78:64995     110.169.240.223:12908   FIN_WAIT2   9992/0          
# udp        0      0 0.0.0.0:53              0.0.0.0:*                           14793/openvpn   
# udp        0      0 0.0.0.0:68              0.0.0.0:*                           682/dhclient3   
# udp        0      0 0.0.0.0:60238           0.0.0.0:*                           14803/openvpn   

die "pass in a mix of: list, update" if @ARGV == 0;

tie my @prev_ports, 'Tie::File', '/tmp/591d435d' or die "tie failed";

open my $ps, '-|', 'netstat -panut' or die "fork: $!";
my @ports;
while (<$ps>) {
    chomp;

    # only interested in:
    # tcp LISTEN state on 0.0.0.0 interface
    # tcp LISTEN state on 127.0.0.1 interface
    # udp on 0.0.0.0 interface
    # ignore udp 68

    my @f = split /\s+/;
    if ($f[3] =~ /(127|0).0.0.(0|1):(\d+)/) {
        my $proto = $f[0];
        my $port = $3;

        next if ($proto eq 'tcp' and $f[5] ne 'LISTEN');
        next if ($proto eq 'udp' and $port == 68);
        next if ($port == 123); # ntp
        push @ports, "$proto-$port";
    }
}

my $diff = Array::Diff->diff(\@prev_ports, \@ports);
if ($diff->count > 0 and grep {$_ eq 'update'} @ARGV) {
    for (["added", $diff->added],
         ["deleted", $diff->deleted],
         ["previous ports", \@prev_ports],
         ["current ports", \@ports]) {

        print $_->[0], ": ", join(', ', sort mysort @{$_->[1]}), "\n";
    }

    @prev_ports = @ports;
}

if (grep {$_ eq 'list'} @ARGV) {
    for my $proto (qw/tcp udp/) {
        print "$proto: ", join(', ', grep s/$proto-//, sort mysort @ports), "\n";
    }
}

sub mysort {
    no warnings 'once';
    my ($left_proto, $left_port) = split /-/, $a;
    my ($right_proto, $right_port) = split /-/, $b;
    return (lc($left_proto) cmp lc($right_proto)) || ($left_port <=> $right_port);
}
        
