#!/usr/bin/env coffee

# url encode/decode

url = require 'url'
direction = 'decode'

handle = (s) ->
    s = s.trim()
    if direction is 'decode'
        p = url.parse s, true
        if p.host
            out = "#{p.protocol}//"
            out += if p.auth then "#{p.auth}@" else ''
            out += "#{p.host}#{p.pathname}"
            if Object.keys(p.query).length
                out += '\n  ?'
                out += ( "#{k}=#{v}" for k,v of p.query ).join '\n  &'
            if p.hash
                out += "\n  #{p.hash}"
            console.log out
        else
            console.log decodeURIComponent s
    else
        console.log encodeURIComponent s

argv = process.argv.slice(2).filter (arg) ->
    if arg.match /^--?d/
        direction = 'decode'
    else if arg.match /^--?e/
        direction = 'encode'
    else
        return true
    return false

if argv.length
    for arg in argv
        handle arg
else
    data = ''
    process.stdin
        .setEncoding 'utf8'
        .on 'readable', ->
            chunk = process.stdin.read()
            data += chunk if chunk
        .on 'end', -> handle data
