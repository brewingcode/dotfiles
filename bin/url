#!/usr/bin/env coffee

# url parse (default), encode, or decode

url = require 'url'
minimist = require 'minimist'
fs = require 'fs'

argv = minimist process.argv.slice(2), boolean: ['e', 'encode', 'd', 'decode']

handle = (s) ->
    s = s.trim()
    if argv.e or argv.encode
        console.log encodeURIComponent s
    else if argv.d or argv.decode
        console.log decodeURIComponent s
    else
        p = url.parse s, true
        out = ''
        out += "#{p.protocol}//" if p.protocol
        out += "#{p.auth}@" if p.auth
        out += p.host if p.host
        out += p.pathname if p.pathname
        if p.query and Object.keys(p.query).length
            out += '\n  ?'
            out += ( "#{k}=#{v}" for k,v of p.query ).join '\n  &'
        if p.hash
            out += "\n  #{p.hash}"
        console.log out

if argv._.length
    handle(arg) for arg in argv._
else
    handle fs.readFileSync '/dev/stdin', 'utf8'
